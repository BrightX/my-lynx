plugins {
    id 'com.android.application'
}

android {
    namespace 'com.bright.lynx'
    compileSdk 33

    defaultConfig {
        applicationId "com.bright.lynx"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file('app-dev-keystore.jks')
            storePassword 'devTest_123'
            keyAlias 'key0'
            keyPassword 'devTest_123'
        }

        release {
            storeFile file('app-dev-keystore.jks')
            storePassword 'devTest_123'
            keyAlias 'key0'
            keyPassword 'devTest_123'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-rules-lynx.pro'
            packagingOptions {
                exclude 'DebugProbesKt.bin'
                exclude 'kotlin/**.kotlin_builtins'
                exclude 'META-INF/**.version'
                exclude 'META-INF/com/android/build/gradle/app-metadata.properties'
            }
            shrinkResources true
            signingConfig signingConfigs.release
        }
    }

    splits {
        abi {
            enable true
            reset()
            // Specifies a list of ABIs that Gradle should create APKs for.
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            // Specifies that we do not want to also generate a universal APK that includes all ABIs.
            universalApk true
        }
    }

    // map for the version code
    project.ext.versionCodes = ['armeabi-v7a': 1, 'arm64-v8a': 2, 'x86': 3, 'x86_64': 4]
    android.applicationVariants.configureEach { variant ->
        // assign different version code for each output
        variant.outputs.each { output ->
            def buildType = variant.name
            def abi = output.getFilter(com.android.build.OutputFile.ABI)
            if (abi == null) abi = "all"
            def versionCode = variant.versionCode * 100 + project.ext.versionCodes.get(abi, 0)

            output.outputFileName = "MyLynx_${variant.versionName}_${buildType}_${abi}.apk"
            output.versionCodeOverride = versionCode
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }

    sourceSets {
        main {
            assets.srcDirs += ['../../dist']
        }
    }
}

dependencies {

    // lynx dependencies
    def lynxVersion = "3.2.0"
    implementation "org.lynxsdk.lynx:lynx:${lynxVersion}"
    implementation "org.lynxsdk.lynx:lynx-jssdk:${lynxVersion}"
    implementation "org.lynxsdk.lynx:lynx-trace:${lynxVersion}"
    implementation "org.lynxsdk.lynx:primjs:2.12.0"

    // integrating image-service
    implementation "org.lynxsdk.lynx:lynx-service-image:${lynxVersion}"

    // image-service dependencies, if not added, images cannot be loaded; if the host APP needs to use other image libraries, you can customize the image-service and remove this dependency
    implementation "com.facebook.fresco:fresco:2.3.0"
    implementation "com.facebook.fresco:animated-gif:2.3.0"
    implementation "com.facebook.fresco:animated-webp:2.3.0"
    implementation "com.facebook.fresco:webpsupport:2.3.0"
    implementation "com.facebook.fresco:animated-base:2.3.0"

    implementation "com.squareup.okhttp3:okhttp:4.9.0"

    // integrating log-service
    implementation "org.lynxsdk.lynx:lynx-service-log:${lynxVersion}"

    // integrating http-service
    implementation "org.lynxsdk.lynx:lynx-service-http:${lynxVersion}"

    // see: https://github.com/Tencent/MMKV/blob/master/README_CN.md#android-%E6%8C%87%E5%8D%97
    // see: https://github.com/Tencent/MMKV/wiki/android_tutorial_cn
    implementation 'com.tencent:mmkv:1.3.12'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
}
